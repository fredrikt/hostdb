#!/usr/local/bin/perl
#
# $Id$
#
# script to read a named.conf without fancy commenting ( /* */ style) and
# with one zone per row in our host database.
#

use strict;
use Config::IniFiles;
#use lib 'blib/lib';
use HOSTDB;
use Net::DNS;

my $debug = 0;
my $file = shift @ARGV;
my $default_ttl;

if (! $file or $#ARGV != -1) {
	die ("Syntax: $0 path/to/named.conf\n");
}

my $hostdbini = Config::IniFiles->new (-file => HOSTDB::get_inifile ());

my $hostdb = HOSTDB::DB->new (dsn => $hostdbini->val ('db', 'dsn'),
			  db => $hostdbini->val ('db', 'database'),
			  user => $hostdbini->val ('db', 'user'),
			  password => $hostdbini->val ('db', 'password'),
			  debug => $debug
			 );

open (FIL, "< $file") or die ("Could not open $file for reading: $!\n");

my $imported = 0;
my $failed = 0;
my @delayed;
while (my $rad = <FIL>) {
	chomp ($rad);

	if ($rad =~ /^zone\s+\"(.+?)\"\s+{\s*type master/) {
		my $name = $1;

		next if ($name eq "0.0.127.in-addr.arpa." or $name eq "0.0.127.in-addr.arpa");
		next if ($name eq "255.in-addr.arpa." or $name eq "255.in-addr.arpa");
		next if ($name eq "0.in-addr.arpa." or $name eq "0.in-addr.arpa");
		next if ($name eq "localhost." or $name eq "localhost");

		my $serial = get_soa_serial ($name);

		if ($serial) {
			warn("zone $name, $serial\n");

			$imported++ if (add_zone ($hostdb, $name, $serial));
		} else {
			$failed++;
		}
	}
}
close (FIL);

print ("Imported $imported zones, failed $failed from file $file\n");

print ("done\n");

sub add_zone
{
	my $hostdb = shift;
	my $zonename = shift;
	my $serial = shift;
	
	if ($hostdb->is_valid_zonename ($zonename)) {
		my $zone = $hostdb->create_zone ();
		
		if (! $zone) {
			warn ("Could not create zone object: $hostdb->{error}\n");
			return undef;
		}

		my $valid = 1;
		$zone->zonename ("$zonename") or warn ($zone->{error}), $valid = 0;
		$zone->serial ($serial) or warn ($zone->{error}), $valid = 0; # XXX FIX ME
		$zone->owner ("import") or warn ($zone->{error}), $valid = 0;
		$zone->delegated ("N") or warn ($zone->{error}), $valid = 0;

		$zone->commit() if ($valid);
	
		undef ($zone);
	
		return $valid;
	}

	return 0;
}

sub get_soa_serial
{
	my $zone = shift;

	my $res = Net::DNS::Resolver->new;
	my $query = $res->query("$zone", "SOA");

	if ($query) {
		foreach my $rr ($query->answer) {
			next unless $rr->type eq "SOA";
			return $rr->serial;
		}
	}

	warn ("Could not get SOA serial for zone '$zone'\n");
	return undef;
}
