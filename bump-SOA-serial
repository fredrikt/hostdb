#!/usr/local/bin/perl -w
#
# $Id$
#
# script to change SOA serial in database
#

use strict;
use Config::IniFiles;
#use lib 'blib/lib';
use HOSTDB;
use Net::DNS;
use Getopt::Std;
use vars qw ($opt_h $opt_d $opt_D $opt_q $opt_f);

getopts ('Dhdf');

my $debug = defined ($opt_d);
my $dns_sync = defined ($opt_D);
my $quiet = defined ($opt_q);
my $force = defined ($opt_f);
my @zones = @ARGV;

my $soa_seq_limit = 80;

if (! $zones[0] or $opt_h) {
	die (<<EOM);
Syntax: $0 [options] zone ...

	options:
		-d	debug
		-h	this help text
		-D	sync SOA serials in database with DNS
		-q	quiet mode
		-f	force update

EOM
}

my $hostdbini = Config::IniFiles->new (-file => HOSTDB::get_inifile ());

my $hostdb = HOSTDB::DB->new (dsn => $hostdbini->val ('db', 'dsn'),
			  db => $hostdbini->val ('db', 'database'),
			  user => $hostdbini->val ('db', 'user'),
			  password => $hostdbini->val ('db', 'password'),
			  debug => $debug
			 );

my $zonename;
my $errors = 0;
foreach $zonename (@zones) {
	if (! $hostdb->clean_domainname ($zonename)) {
		warn ("Invalid zone name '$zonename'\n");
		$errors++;
		next;
	}

	my $zone = $hostdb->findzonebyname ($zonename);

	if (! defined ($zone)) {
		warn ("$0: Zone not found '$zonename'\n");
		$errors++;
		next;
	}

	my $old_serial = $zone->serial ();
	my $new_serial;

	if ($dns_sync) {
		$new_serial = get_dns_soa_serial ($zone);
		$errors++, next if (! defined ($new_serial));
	} else {
		my ($sec, $min, $hour, $mday, $mon, $year, $yday, $isdst) = localtime ();
		$year += 1900;  # yes, this is Y2K safe (why do you even bother? this was written
				# in the year of 2002)
		my $today = sprintf ("%.4d%.2d%.2d", $year, $mon, $mday);

		if ($old_serial =~ /^$today(\d\d)/) {
			my $n = int ($1);
			if ($n >= 99) {
				# should never be more of course, but anyways...
				warn ("$0: Can't increase SOA for zone $zonename any more today. Sorry.");
				next;
			}
			if ($n > $soa_seq_limit) {
				if (! $force) {
					warn ("$0: SOA serial for zone $zonename has todays date but " .
					      "sequence number is > $soa_seq_limit ($n) - won't update unless " .
					      "you force me to.");
					next;
				}
			}
			$new_serial = sprintf("%s%.2d", $today, $n + 1);
		} else {
			$new_serial = "${today}01";
		}
	}
	
	if ($old_serial ne $new_serial) {
		$zone->serial ($new_serial) or warn ($zone->{error}), $errors++, next;
		$zone->commit () or warn ($zone->{error}), $errors++, next;
		
		print ("$zonename: Changed SOA serial from $old_serial to $new_serial\n") if (! $quiet);
	}
}


sub get_dns_soa_serial
{
	my $zone = shift;

	my $res = Net::DNS::Resolver->new;
	my $query = $res->query("$zone", "SOA");

	if ($query) {
		foreach my $rr ($query->answer) {
			next unless $rr->type eq "SOA";
			return $rr->serial;
		}
	}

	warn ("$0: Could not get SOA serial for zone '$zone'\n");
	return undef;
}
