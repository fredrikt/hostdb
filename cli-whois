#!/usr/local/bin/perl -w
#
# $Id$
#
# script to search for IP addresses or mac addresses
#

use strict;
use HOSTDB;

my $debug = 0;
if ($ARGV[0] eq "-d") {
	shift (@ARGV);
	$debug = 1;
}

my $hostdb = HOSTDB::DB->new (inifile => HOSTDB::get_inifile (),
			      debug => $debug
			     );

my @searchfor = @ARGV;

if ($#searchfor == -1) {
	die ("Syntax: $0 [-d] <IP/FQDN/MAC> ...\n");
}

my $first = 1;
while ($ARGV[0]) {
	my @host_refs;
	my $search_for = shift;

	my $t = $search_for;

	@host_refs = $hostdb->findhost ('guess', $search_for);
	if ($hostdb->{error}) {
		die ("$0: $hostdb->{error}\n");
	}

	if (@host_refs) {
		foreach my $host (@host_refs) {
			print ("Host :\n\n");

		
			# interpolation
			my $id = $host->id ();
			my $parent = $host->partof ()?$host->partof ():"-";
			my $ip = $host->ip ();
			my $mac = $host->mac_address () || "n/a";
			my $mac_ts = $host->mac_address_ts () || "no timestamp";
			my $hostname = $host->hostname () || 'NULL';
			my $comment = $host->comment () || 'NULL';
			my $owner = $host->owner ();
			my $dhcpstatus = $host->dhcpstatus ();
			my $dhcpmode = $host->dhcpmode ();
			my $dnsstatus = $host->dnsstatus ();
			my $dnsmode = $host->dnsmode ();
			my $ttl = $host->ttl () || 'default';
			my $profile = $host->profile ();
			my $zone = $host->dnszone () || 'NULL';
			my $manual_zone = $host->manual_dnszone () || 'NULL';

			print (<<EOH);
	ID	$id
	Parent	$parent
	---
	
	DNS :
	  IP address	$ip
	  Hostname	$hostname
	  Zone		$zone
	  Manual zone	$manual_zone
	  TTL		$ttl
	  Mode		$dnsmode
	  Status	$dnsstatus
	
	DHCP :
	  MAC address	$mac (last seen $mac_ts)
	  Mode		$dhcpmode
	  Status	$dhcpstatus

	General :
	  Profile	$profile
	  Comment	$comment
	  Owner		$owner

EOH
			my @attrs = $host->init_attributes();
			my $numattrs = scalar @attrs;

			if ($numattrs > 0) {
				print (<<EOH);

	Host has $numattrs attributes :
EOH

				my $lastsection = '';
				
				foreach my $attr (@attrs) {
					my $key = $attr->key ();
					my $section = $attr->section ();
					my $value = $attr->get ();
					
					if ($section ne $lastsection) {
						print ("\n	  [$section]\n");
					}
					
					printf ("	    %-20s = %s\n", $key, $value);
				}
				
				print ("\n");
			}
			
			my $subnet = $hostdb->findsubnetbyip ($host->ip ());
		
			if ($subnet) {
				printf ("	%-23s %-20s %s\n", "subnet", "netmask", "description");
				
				printf "	%-23s %-20s %s\n", $subnet->subnet(),
						$subnet->netmask (), $subnet->description ();
		
				print ("\n");
			} else {
				print ("Could not find a subnet in database\n\n");
			}
		}
			
		print ("---\n\n");
	} else {
		print ("No match\n");
	}
}
